## Load scd function and its completion helper according to repository layout.
## Keep zsh-completion in fpath in case compinit is called after this script.
if [[ ${+functions[scd]} == 1 ]]; then
    : "do nothing because scd is already defined."
else
    _zshrc_scd_fpath=( $fpath )
    _zshrc_scd_basedir=${${(%):-%x}:A:h:h}
    fpath=( ${_zshrc_scd_basedir}/bin
            ${_zshrc_scd_basedir}/zsh-completion
            $fpath )
    autoload -Uz +X scd
    if [[ ${+functions[compdef]} == 1 ]]; then
        autoload -Uz +X _scd
        compdef _scd scd
    fi
    fpath=( $_zshrc_scd_fpath ${_zshrc_scd_basedir}/zsh-completion )
    unset -m "_zshrc_scd_*"
fi


## If the scd function exists, define a change-directory-hook function
## to record visited directories in the scd index.
if [[ ${+functions[scd]} == 1 ]]; then
    scd_chpwd_hook() { scd --add $PWD }
    autoload -Uz add-zsh-hook
    add-zsh-hook chpwd scd_chpwd_hook
else
    print -u2 "scd could not be found neither in FPATH nor in PATH."
fi


## Load the directory aliases created by scd if any.
if [[ -s ~/.scdalias.zsh ]]; then
    source ~/.scdalias.zsh
fi
